plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "1.9.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"

    implementation project(":api")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('cleanJar') {
    dependsOn 'clean'
    dependsOn 'shadowJar'
    tasks.findByName('shadowJar').mustRunAfter 'clean'
}
kotlin {
    jvmToolchain(17)
}

shadowJar {
    dependencies {
        exclude(dependency("org.jetbrains.kotlin:kotlin-stdlib:1.9.0"))
    }
    archiveFileName = "Telepartacyja-${version}.jar"
}